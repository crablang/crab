// This test ensures that the correct style is applied to the crablang logo in the sidebar.
goto: "file://" + |DOC_PATH| + "/test_docs/index.html"

define-function: (
    "check-logo",
    (theme, filter),
    block {
        // Going to the doc page.
        goto: "file://" + |DOC_PATH| + "/test_docs/index.html"
        // Changing theme.
        local-storage: {"crablangdoc-theme": |theme|, "crablangdoc-use-system-theme": "false"}
        reload:
        assert-css: (".crablang-logo", {"filter": |filter|})
        // Going to the source code page.
        goto: "file://" + |DOC_PATH| + "/src/test_docs/lib.rs.html"
        // Changing theme (since it's local files, the local storage works by folder).
        local-storage: {"crablangdoc-theme": |theme|, "crablangdoc-use-system-theme": "false"}
        reload:
        assert-css: (".crablang-logo", {"filter": |filter|})
        // Now we check that the non-crablang logos don't have a CSS filter set.
        goto: "file://" + |DOC_PATH| + "/huge_logo/index.html"
        // Changing theme on the new page (again...).
        local-storage: {"crablangdoc-theme": |theme|, "crablangdoc-use-system-theme": "false"}
        reload:
        // Check there is no crablang logo
        assert-false: ".crablang-logo"
        // Check there is no filter.
        assert-css: (".sidebar .logo-container img", {"filter": "none"})
    },
)

call-function: (
    "check-logo",
    {
        "theme": "ayu",
        "filter": "drop-shadow(rgb(255, 255, 255) 1px 0px 0px) " +
            "drop-shadow(rgb(255, 255, 255) 0px 1px 0px) " +
            "drop-shadow(rgb(255, 255, 255) -1px 0px 0px) " +
            "drop-shadow(rgb(255, 255, 255) 0px -1px 0px)",
    },
)
call-function: (
    "check-logo",
    {
        "theme": "dark",
        "filter": "drop-shadow(rgb(255, 255, 255) 1px 0px 0px) " +
            "drop-shadow(rgb(255, 255, 255) 0px 1px 0px) " +
            "drop-shadow(rgb(255, 255, 255) -1px 0px 0px) " +
            "drop-shadow(rgb(255, 255, 255) 0px -1px 0px)",
    },
)
call-function: (
    "check-logo",
    {
        "theme": "light",
        "filter": "none",
    },
)
