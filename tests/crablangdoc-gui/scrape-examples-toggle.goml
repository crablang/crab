// This tests checks that the "scraped examples" toggle is working as expected.
goto: "file://" + |DOC_PATH| + "/scrape_examples/fn.test_many.html"

// Checking the color of the toggle line.
show-text: true
define-function: (
    "check-color",
    (theme, toggle_line_color, toggle_line_hover_color),
    block {
        local-storage: {"crablangdoc-theme": |theme|, "crablangdoc-use-system-theme": "false"}
        reload:

        // Clicking "More examples..." will open additional examples
        assert-attribute-false: (".more-examples-toggle", {"open": ""})
        click: ".more-examples-toggle"
        assert-attribute: (".more-examples-toggle", {"open": ""})

        assert-css: (".toggle-line-inner", {"background-color": |toggle_line_color|}, ALL)
        move-cursor-to: ".toggle-line"
        assert-css: (
            ".toggle-line:hover .toggle-line-inner",
            {"background-color": |toggle_line_hover_color|},
        )
        // Moving cursor away from the toggle line to prevent disrupting next test.
        move-cursor-to: ".search-input"
    },
)

call-function: ("check-color", {
    "theme": "ayu",
    "toggle_line_color": "rgb(153, 153, 153)",
    "toggle_line_hover_color": "rgb(197, 197, 197)",
})
call-function: ("check-color", {
    "theme": "dark",
    "toggle_line_color": "rgb(153, 153, 153)",
    "toggle_line_hover_color": "rgb(197, 197, 197)",
})
call-function: ("check-color", {
    "theme": "light",
    "toggle_line_color": "rgb(204, 204, 204)",
    "toggle_line_hover_color": "rgb(153, 153, 153)",
})

// Toggling all docs will close additional examples
click: "#toggle-all-docs"
assert-attribute-false: (".more-examples-toggle", {"open": ""})

// After re-opening the docs, the additional examples should stay closed
click: "#toggle-all-docs"
assert-attribute-false: (".more-examples-toggle", {"open": ""})
